script_enemy_main{
#include_function ".\FUNC\Zako.dnh"
	let count = 0;
	let Level = GetCommonDataDefault( "Level", 1 );
	let Argv = GetArgumentDefault( 0 );
	task Shot(){
		loop( Level ){
			WayShot01( 7, 7, 1.6, AQUA05, 10 );
			PlaySEEx( seShot3 );
			Wait( 10 );
		}
	}
	task Behavior(){
		yield;
		let dir = [ -1, 1 ][ GetX() < GetCenterX() ];
		SetMovePosition03( GetX() + ( GetCommonDataDefault( "GlobalCount", count ) % 32 + 64 ) * dir, Max( GetPlayerY() / [ 3, 2 ][ Argv % 2 ], GetClipMinY() + 8 ), 20, Smooth( 1, [ 3, 5 ][ Argv % 2 ], GetPlayerY(), GetClipMaxY() ) );
		WaitSpeedDown( 2.5 );
		SetShotDirectionType( PLAYER );
		if( Level != 0 ){ Shot(); }
		WaitZeroSpeed();
		SetMovePosition03( GetX() - 12 * dir, GetY() + 8, 20, 0.5 );
		WaitZeroSpeed();
		Wait( 20 );
		SetShotDirectionType( PLAYER );
		let speed = 1;
		loop( [ 0, 1, 3, 5 ][ Level ] ){
			if( Level == 3 ){ WayShot01( 15, 15, speed, WHITE05, 5 ); }
			else{ CreateShot01( GetX(), GetY(), speed, dir * 2, WHITE05, 5 ); }
			speed += 0.8;
		}
		if( Level != 0 ){ PlaySEEx( seShot1 ); }
		SetAngle( [ 150, 30 ][ GetX() < GetCenterX() ] );
		while( GetSpeed() < 2 ){
			SetSpeed( GetSpeed() + 0.1 );
			Wait( 2 );
		}
	}
	@Initialize{
		SetLife( 10 );
		SetDamageRate( 0, 10 );
		Behavior();
	}
	@MainLoop{
		if( count == 3 ){ SetDamageRate( 100, 100 ); }
		SEShotHit( seDamage, count );
		Collision( 20, 8 );
		Gone();
		yield;
		count++;
	}
	@Finalize{ if( count > 180 || Argv % 3 == 0 ){ DropItem( 1, 8 ); } }
	@DrawLoop{
		SetGraphicRect( 1, 1, 32, 32 );
		DrawYosei( imgEnemyYosei1, count );
	}
}
