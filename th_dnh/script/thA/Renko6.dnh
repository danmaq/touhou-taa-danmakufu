script_enemy_main{
#include_function ".\FUNC\Boss.dnh"
	let count = 0;
	let PrevTime = GetTimer();
	task Behavior(){
		Wait( 150 );
		SetShotDirectionType( PLAYER );
		let pattern = 0;
		loop{
			loop( 10 ){
				ascent( let i in 1..3 ){ CircleShot01( 15, i * 1.5 * ( pattern % 2 + 1 ), ORANGE11, 15 ); }
				PlaySEEx( seShot1 );
				Wait( 7 );
			}
			ExMove( false, count );
			Wait( [ 7, 35 ][ pattern % 2 ] );
			pattern++;
		}
	}
	@Initialize{
		SetLife( 2600 );
		SetTimer( 60 );
		SetInvincibility( 30 );
		SetDamageRate( 100, 5 );
		SetMovePosition03( GetCenterX(), GetClipMinY() + 96, 20, 2.5 );
		SetEnemyMarker( true );
		Behavior();
	}
	@MainLoop{
		if( PrevTime != GetTimer() ){
			PrevTime = GetTimer();
			if( GetTimer() < 10 ){ PlaySEEx( seTimeOut ); }
		}
		SEShotHitBoss( seDamage, count );
		Collision( 32, 16 );
		yield;
		count++;
	}
	@DrawLoop{
		DrawCircle( imgSystemCircleBig, count, 256 );
		DrawBossChr( imgEnemyRenko, 1, 1, 43, 72, count );
	}
}
