script_enemy_main{
#include_function ".\FUNC\Boss.dnh"
	let count = 0;
	task Behavior(){
		yield;
		PlaySEEx( seBossExplode );
		CreateEventFromScript( "Talk" );
		while( GetEventStep() != 1 ){ yield; }
		SetCommonData( "Result", true );
		let clear = 40000000;
		let point = GetPoint() * 50000 - GetCommonDataDefault( "ResultPoint", 0 );
		let graze = GetGraze() * 500 - GetCommonDataDefault( "ResultGraze", 0 );
		let rank = [ 50, 100, 120, 150 ][ GetCommonDataDefault( "Level", 1 ) ];
		let total = ( clear + point + graze ) * ( rank / 100 );
		SetCommonData( "ResultClear", clear );
		SetCommonData( "ResultPoint", point );
		SetCommonData( "ResultGraze", graze );
		SetCommonData( "ResultRank", rank );
		SetCommonData( "ResultTotal", total );
		AddScore( total );
	}
	@Initialize{
		SetLife( 1 );
		SetX( -128 );
		SetY( -128 );
		LoadSE( seBossExplode );
		ForbidBomb( true );
		ForbidShot( true );
		Behavior();
	}
	@MainLoop{
		yield;
		if( !OnEvent() ){ VanishEnemy(); }
	}
}
script_event Talk{
#include_function ".\FUNC\Event.dnh"
	@MainLoop{
		alternative( GetPlayerType() )
		case( REIMU_A, REIMU_B ){
			SetChar( LEFT, imgCutInReimu );
			SetGraphicRect( LEFT, 1, 1, 192, 320 );
			SetChar( RIGHT, imgCutInLeilaCry );
			SetGraphicRect( RIGHT, 1, 1, 192, 304 );
			Player();
			TextOut( "\c[BLUE]全然強く無いわね、\n数撃ちゃいいってもんじゃないわ" );
			Enemy();
			TextOut( "\c[RED]うぅ、私楽器使えないし" );
			Player();
			SetStep( 1 );
			SetChar( LEFT, imgCutInReimu4 );
			TextOut( "\c[BLUE]はいはい負け惜しみよくない\nそれじゃさっさと地下行かせてもらうわね" );
		}
		case( MARISA_A, MARISA_B ){
			SetChar( LEFT, imgCutInMarisa2 );
			SetGraphicRect( LEFT, 1, 1, 192, 296 );
			SetChar( RIGHT, imgCutInLeilaCry );
			SetGraphicRect( RIGHT, 1, 1, 192, 304 );
			Player();
			TextOut( "\c[BLUE]なんだそりゃ\n５枚も用意しててその程度かよ" );
			Enemy();
			TextOut( "\c[RED]うぅ、なんか埃っぽいし" );
			Player();
			SetStep( 1 );
			SetChar( LEFT, imgCutInMarisa3 );
			TextOut( "\c[BLUE]さっさと掃除しろ。\nちなみにこの箒は貸さないぜ" );
		}
		End();
	}
}
