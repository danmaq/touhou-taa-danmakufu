script_enemy_main{
#include_function ".\FUNC\Zako.dnh"
	let count = 0;
	let Level = GetCommonDataDefault( "Level", 1 );
	let Argv = GetArgumentDefault( 0 );
	task Behavior(){
		yield;
		SetShotDirectionType( PLAYER );
		SetSpeed( 2 );
		SetAngle( 90 );
		Wait( 30 );
		let dir = [ -1, 1 ][ Argv % 2 ];
		loop( 2 ){
			loop( 100 ){
				SetAngle( GetAngle() - dir );
				yield;
			}
			let speed = 0.5;
			loop( Level + 1 ){
				CreateShot01( GetX(), GetY(), speed, 0, WHITE05, 5 );
				speed += 0.4;
			}
			PlaySEEx( seShot1 );
			dir = -dir;
		}
	}
	@Initialize{
		SetLife( 3 );
		Behavior();
	}
	@MainLoop{
		SEShotHit( seDamage, count );
		Collision( 20, 8 );
		Gone();
		yield;
		count++;
	}
	@Finalize{
		if( !IsOut( 32 ) && GetY() < GetCenterY() + 64 && ( Level != 0 || Argv % 2 == 0 ) ){
			let color = [ BLUE04, RED04 ][ Argv % 2 ];
			ascent( let i in 1..[ 1, 2, 5, 6 ][ Level ] ){ WayShot02( 120, 30, 0, 0.05, i / 4, color, 8 + i * 3 ); }
			PlaySEEx( seShot3 );
		}
		if( count > 180 || Argv % 4 == 0 ){ DropItem( 1, 8 ); }
	}
	@DrawLoop{
		SetGraphicRect( 1, 1, 32, 32 );
		DrawYosei( imgEnemyYosei5, count );
	}
}
