#include_function ".\FUNC\Common.dnh"
#include_function ".\BGM\index.dnh"
#include_function ".\SE\index.dnh"
function IsOutPos( let x, y, deadline ){ return x < GetClipMinX() - deadline || x > GetClipMaxX() + deadline || y < GetClipMinY() - deadline || y > GetClipMaxY() + deadline; }
function IsPushDirKey(){ return GetKeyState( VK_UP ) == KEY_PUSH || GetKeyState( VK_DOWN ) == KEY_PUSH || GetKeyState( VK_LEFT ) == KEY_PUSH || GetKeyState( VK_RIGHT ) == KEY_PUSH; }
function RandBlur( let expr ){ return rand( -expr, expr ); }
function _DrawChr( let file, let x, let y ){
	SetTexture( file );
	DrawGraphic( x, y );
}
function _DrawChrRect( let file, let dstx, let dsty, let srcx1, let srcy1, let srcx2, let srcy2 ){
	SetGraphicRect( srcx1, srcy1, srcx2, srcy2 );
	_DrawChr( file, dstx, dsty );
}
function DrawCenter( let file ){ _DrawChr( file, GetCenterX(), GetCenterY() ); }
function DrawPlayer( let file ){ _DrawChr( file, GetPlayerX(), GetPlayerY() ); }
function SetColorA( let r, let g, let b ){
	if( OnBomb() ){ SetColor( r / [ 3, 1 ][ IsReimu() ], g / [ 1, 3 ][ IsReimu() ], b / [ 1, 3 ][ IsReimu() ] ); }
	else{ SetColor( r, g, b ); }
}
function DrawPlayerCircle( let file, let count ){
	SetAlpha( 64 );
	SetGraphicAngle( 0, 0, count * 16 );
	SetGraphicRect( 0, 0, 64, 64 );
	DrawPlayer( file );
	SetGraphicAngle( 0, 0, 0 );
	SetAlpha( 255 );
}
function DeleteGraphicList( let GraphicFileList, let SEFileList ){
	ascent( let i in 0..2 ){
		ascent( let j in 0..[ length( GraphicFileList ), length( SEFileList ) ][ i ] ){
			alternative( i )
			case( 0 ){ DeleteGraphic( GraphicFileList[ j ] ); }
			case( 1 ){ DeleteSE( SEFileList[ j ] ); }
		}
	}
}
function MuteMusic(){ ascent( let i in 1..18 ){ DeleteMusic( bgmList[ i ] ); } }
function Wait( let frame ){ if( frame > 0 ){ loop( frame ){ yield }; } }
function WaitZeroEnemy(){ while( GetEnemyNum() != 0 ){ yield; } }
task PlayMusicEx( let no ){
	ascent( let i in 1..19 ){ if( i != no ){ DeleteMusic( bgmList[ i ] ); } }
	if( no != 0 ){ SetCommonData( "NowBGM", no ); }
	Wait( 30 );
	if( GetCommonDataDefault( "BGMEnable", true ) ){ PlayMusic( bgmList[ no ] ); }
}
function PlaySEEx( let file ){ if( GetCommonDataDefault( "SEEnable", true ) ){ PlaySE( file ); } }
function SaveEx(){ if( !IsReplay() ){ SaveCommonData(); } }
